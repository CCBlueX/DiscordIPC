plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

version = '0.6.0'
group = 'com.jagrosh'
description = 'Connect locally to the Discord client using IPC for a subset of RPC features like Rich Presence and Activity Join/Spectate'
archivesBaseName = 'DiscordIPC'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    shadeOnly
    shade
    implementation.extendsFrom shade
}

repositories {
    mavenCentral()
}

dependencies {
    shade group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    shade group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    shade group: 'com.kohlschutter.junixsocket', name: 'junixsocket-common', version: '2.4.0'
    shade group: 'com.kohlschutter.junixsocket', name: 'junixsocket-native-common', version: '2.4.0'

    // SLF4J Test Bindings (Enable depending on test conditions)
    runtimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.36'
}

jar.manifest.mainAttributes([
        'Implementation-Title'  : project.name,
        'Implementation-Version': project.version
])

shadowJar {
    configurations = [project.configurations.shade, project.configurations.shadeOnly]
    archiveClassifier.set('')
    exclude 'META-INF/versions/**'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier.set('javadoc')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set('sources')
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

assemble.dependsOn javadocJar, sourcesJar

[compileJava].each {
    it.options.encoding = 'UTF-8'
    it.options.deprecation = true
    it.options.fork = true
}
